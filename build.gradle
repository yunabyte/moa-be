plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'

	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.moa'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	configureEach {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// DB & JPA
	implementation 'com.mysql:mysql-connector-j'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	// QueryDSL
	implementation "com.querydsl:querydsl-core:5.1.0"
	implementation "com.querydsl:querydsl-jpa:5.1.0:jakarta"
	annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

	// Swagger
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8"

	// 기타
	implementation "org.apache.httpcomponents.client5:httpclient5:5.4.4" // RestTemplate
	implementation 'com.navercorp.lucy:lucy-xss:1.6.3' // XSS Filtering

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  // Prometheus / Micrometer
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'io.micrometer:micrometer-registry-prometheus'
}

spotless {
	java {
		googleJavaFormat('1.17.0')
		target 'src/**/*.java'
	}
}

checkstyle {
	toolVersion = '10.23.1'
	configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
	configProperties = [
			'checkstyle.suppressions.file': file("$rootDir/config/checkstyle/suppressions.xml")
	]
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named("checkstyleMain") {
  dependsOn "compileJava", "compileTestJava"
}

// QueryDSL Q파일 생성용
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

sourceSets {
	main {
		java {
			srcDir querydslDir
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.generatedSourceOutputDirectory.set(querydslDir)
}

compileJava {
	options.annotationProcessorPath = configurations.annotationProcessor
}
